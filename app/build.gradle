plugins {
    id('com.android.application')
    id('org.jetbrains.kotlin.android')
}

android {
    namespace = 'es.upm.ies.surco'
    compileSdk = 36

    defaultConfig {
        applicationId = 'es.upm.ies.surco'
        minSdk = 26
        targetSdk = 36
        versionCode = 6
        versionName = "0.2.1"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "GITHUB_URL", "\"https://github.com/isi-ies-group/VIPV-surCO-Client\"")
        buildConfigField("String", "CONTACT_EMAIL", "\"ies+apps+surco@upm.es\"")

        resConfigs "en", "es"
    }

    buildTypes {
        release {
            minifyEnabled(true)
            shrinkResources(true)
            proguardFiles(
                getDefaultProguardFile('proguard-android-optimize.txt'),
                'proguard-rules.pro'
            )
            // BuildConfigField for production server URL
            buildConfigField("String", "SERVER_URL", "\"https://surco.ies.upm.es/\"")
            signingConfig = signingConfigs.debug
        }
        debug {
            // BuildConfigField for debug server URL
            buildConfigField("String", "SERVER_URL", "\"http://192.168.1.42:5000\"")
            // BuildConfigField for in-code debug flag
            buildConfigField("Boolean", "DEBUG", "true")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
    }
}

dependencies {
    // General
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.work:work-runtime-ktx:2.10.1'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Connectivity
    implementation 'org.altbeacon:android-beacon-library:2.21.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'

    // UI
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.8.8'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.9.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.9.1'

    // HTTP & API
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'

    // Types
    implementation 'com.google.code.gson:gson:2.12.1'
    implementation 'org.apache.commons:commons-collections4:4.4'

    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.10.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.10.1'

    // Security
    implementation 'com.lambdapioneer.argon2kt:argon2kt:1.6.0'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.14.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
